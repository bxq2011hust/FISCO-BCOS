include(ExternalProject)

ExternalProject_Add(wabt
    PREFIX ${CMAKE_SOURCE_DIR}/deps
    DOWNLOAD_NAME wabt_1.0.19.tar.gz
    DOWNLOAD_NO_PROGRESS 1
    URL https://github.com/WebAssembly/wabt/archive/cd5ff133f84854f0b269f5cb06193ad8205f05d3.tar.gz
    URL_HASH SHA256=c8704a45585d4f23b2368602a3d4583a1fab5de620e024e6fe002846b1537033
    BUILD_IN_SOURCE 0
    CMAKE_ARGS -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
    -DCMAKE_POSITION_INDEPENDENT_CODE=ON
    -DBUILD_TESTS=OFF -DBUILD_TOOLS=OFF -DBUILD_LIBWASM=OFF
    INSTALL_COMMAND ""
    LOG_CONFIGURE 1
    LOG_DOWNLOAD 1
    LOG_UPDATE 1
    LOG_BUILD 1
    LOG_INSTALL 0
    BUILD_BYPRODUCTS <BINARY_DIR>/libwabt.a
)

ExternalProject_Get_Property(wabt SOURCE_DIR)
ExternalProject_Get_Property(wabt BINARY_DIR)
add_library(WABT STATIC IMPORTED GLOBAL)

set(WABT_LIBRARY ${BINARY_DIR}/libwabt.a)
set(WABT_INCLUDE_DIR ${SOURCE_DIR}/ ${BINARY_DIR}/)
file(MAKE_DIRECTORY ${WABT_INCLUDE_DIR})  # Must exist.

set_property(TARGET WABT PROPERTY IMPORTED_LOCATION ${WABT_LIBRARY})
set_property(TARGET WABT PROPERTY INTERFACE_INCLUDE_DIRECTORIES ${WABT_INCLUDE_DIR})

add_dependencies(WABT wabt)
unset(SOURCE_DIR)
