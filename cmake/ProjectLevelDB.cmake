include(ExternalProject)

ExternalProject_Add(leveldb
    PREFIX ${CMAKE_SOURCE_DIR}/deps
    DOWNLOAD_NAME leveldb-1.20.tar.gz
    DOWNLOAD_NO_PROGRESS 1
    GIT_REPOSITORY https://github.com/google/leveldb.git
    GIT_TAG 808e59e
    GIT_SHALLOW true
    CMAKE_COMMAND ${CMAKE_COMMAND}
    CMAKE_ARGS -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
    -DCMAKE_POSITION_INDEPENDENT_CODE=${BUILD_SHARED_LIBS}
    ${_only_release_configuration}
    -DLEVELDB_BUILD_TESTS=Off
    -DLEVELDB_BUILD_BENCHMARKS=Off
    -DBUILD_SHARED_LIBS=Off
    -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
    -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
    BUILD_COMMAND ""
    UPDATE_DISCONNECTED 1
    ${_overwrite_install_command}
    BUILD_IN_SOURCE 1
    LOG_CONFIGURE 1
    LOG_DOWNLOAD 1
    LOG_UPDATE 1
    LOG_BUILD 1
    LOG_INSTALL 1
    BUILD_BYPRODUCTS <INSTALL_DIR>/${CMAKE_INSTALL_LIBDIR}/libleveldb.a
)

ExternalProject_Get_Property(leveldb INSTALL_DIR)
add_library(LevelDB STATIC IMPORTED GLOBAL)

set(LEVELDB_INCLUDE_DIR ${INSTALL_DIR}/include/)
set(LEVELDB_LIBRARY ${INSTALL_DIR}/${CMAKE_INSTALL_LIBDIR}/libleveldb.a)
file(MAKE_DIRECTORY ${LEVELDB_INCLUDE_DIR})  # Must exist.

set_property(TARGET LevelDB PROPERTY IMPORTED_LOCATION ${LEVELDB_LIBRARY})
set_property(TARGET LevelDB PROPERTY INTERFACE_INCLUDE_DIRECTORIES ${LEVELDB_INCLUDE_DIR})
set_property(TARGET LevelDB PROPERTY INTERFACE_LINK_LIBRARIES Snappy)

add_dependencies(LevelDB leveldb)
unset(INSTALL_DIR)
